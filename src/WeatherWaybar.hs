module WeatherWaybar where

import Data.Aeson (FromJSON, ToJSON (toJSON), defaultOptions, eitherDecode, encode, genericParseJSON, genericToJSON, parseJSON, withObject, (.:))
import Data.Aeson.Types (Parser, Value)
import Network.HTTP.Client
import Network.HTTP.Client.TLS (tlsManagerSettings)
import RIO
import qualified RIO.ByteString.Lazy as BL
import System.IO (print)

runMain :: IO ()
runMain = do
  weather <- getWeather
  either print (buildWaybarJson >>> encode >>> BL.putStr) weather

data CurrentCondition = CurrentCondition
  { feelsLikeC :: String,
    humidity :: String,
    cloudCover :: String,
    weatherCode :: String
  }
  deriving (Eq, Show, Generic)

parseCurrentCondition :: Value -> Parser CurrentCondition
parseCurrentCondition = withObject "CurrentCondition" $ \obj -> do
  feelsLikeC <- obj .: "FeelsLikeC"
  humidity <- obj .: "humidity"
  cloudCover <- obj .: "cloudcover"
  weatherCode <- obj .: "weatherCode"
  pure (CurrentCondition feelsLikeC humidity cloudCover weatherCode)

instance FromJSON CurrentCondition where
  parseJSON = parseCurrentCondition

instance ToJSON CurrentCondition where
  toJSON = genericToJSON defaultOptions

newtype Region = Region {unRegion :: String}
  deriving (Eq, Show, Generic, IsString)

parseRegion :: Value -> Parser Region
parseRegion = withObject "Region" $ \obj -> do
  region <- obj .: "value"
  pure (Region region)

instance FromJSON Region where
  parseJSON = parseRegion

instance ToJSON Region where
  toJSON = genericToJSON defaultOptions

newtype NearestArea = NearestArea
  { region :: [Region]
  }
  deriving (Eq, Show, Generic)

instance FromJSON NearestArea where
  parseJSON = genericParseJSON defaultOptions

instance ToJSON NearestArea where
  toJSON = genericToJSON defaultOptions

data WeatherResp = WeatherResp
  { currentCondition :: [CurrentCondition],
    nearestArea :: [NearestArea]
  }
  deriving (Eq, Show, Generic)

parseWeatherResp :: Value -> Parser WeatherResp
parseWeatherResp = withObject "WeatherResp" $ \obj -> do
  currentCondition <- obj .: "current_condition"
  nearestArea <- obj .: "nearest_area"
  pure (WeatherResp currentCondition nearestArea)

instance FromJSON WeatherResp where
  parseJSON = parseWeatherResp

instance ToJSON WeatherResp where
  toJSON = genericToJSON defaultOptions

getWeather :: IO (Either String WeatherResp)
getWeather = do
  let url = "https://wttr.in/?format=j1"
  resp <- httpLbs (parseRequest_ url) =<< newManager tlsManagerSettings
  return $ eitherDecode $ responseBody resp

data WaybarJson = WaybarJson
  { text :: String,
    tooltip :: String
  }
  deriving (Eq, Show, Generic)

instance FromJSON WaybarJson where
  parseJSON = genericParseJSON defaultOptions

instance ToJSON WaybarJson where
  toJSON = genericToJSON defaultOptions

buildWaybarJson :: WeatherResp -> WaybarJson
buildWaybarJson
  WeatherResp
    { currentCondition =
        [CurrentCondition {feelsLikeC, humidity, weatherCode, cloudCover}],
      nearestArea =
        [NearestArea {region = [Region {unRegion = region}]}]
    } = do
    let weatherEmoji = maybe "" unEmoji $ getEmojiByWeatherCode constWeatherCodes $ WeatherCode weatherCode
    WaybarJson
      { tooltip =
          mconcat
            [ "Feels like ",
              feelsLikeC,
              "Â°C\n",
              humidity,
              "% humidity\n",
              cloudCover,
              "% cloud cover\n",
              "Weather code: ",
              weatherCode,
              " ",
              weatherEmoji,
              "\nRegion: ",
              region
            ],
        text =
          mconcat
            [ weatherEmoji,
              feelsLikeC,
              "Â°C "
            ]
      }
buildWaybarJson err = error $ "Unexpected response from wttr.in" ++ show err

getEmojiByWeatherCode :: [(WeatherCode, Emoji)] -> WeatherCode -> Maybe Emoji
getEmojiByWeatherCode weatherCodes weatherCode =
  lookup weatherCode weatherCodes

newtype Emoji = Emoji {unEmoji :: String}
  deriving (Eq, Show, Generic, IsString)

newtype WeatherCode = WeatherCode {unWeatherCode :: String}
  deriving (Eq, Show, Generic, IsString)

constWeatherCodes :: [(WeatherCode, Emoji)]
constWeatherCodes =
  [ ("113", "â˜€ï¸"),
    ("116", "â›…ï¸"),
    ("119", "â˜ï¸"),
    ("122", "â˜ï¸"),
    ("143", "ğŸŒ«"),
    ("176", "ğŸŒ¦"),
    ("179", "ğŸŒ§"),
    ("182", "ğŸŒ§"),
    ("185", "ğŸŒ§"),
    ("200", "â›ˆ"),
    ("227", "ğŸŒ¨"),
    ("230", "â„ï¸"),
    ("248", "ğŸŒ«"),
    ("260", "ğŸŒ«"),
    ("263", "ğŸŒ¦"),
    ("266", "ğŸŒ¦"),
    ("281", "ğŸŒ§"),
    ("284", "ğŸŒ§"),
    ("293", "ğŸŒ¦"),
    ("296", "ğŸŒ¦"),
    ("299", "ğŸŒ§"),
    ("302", "ğŸŒ§"),
    ("305", "ğŸŒ§"),
    ("308", "ğŸŒ§"),
    ("311", "ğŸŒ§"),
    ("314", "ğŸŒ§"),
    ("317", "ğŸŒ§"),
    ("320", "ğŸŒ¨"),
    ("323", "ğŸŒ¨"),
    ("326", "ğŸŒ¨"),
    ("329", "â„ï¸"),
    ("332", "â„ï¸"),
    ("335", "â„ï¸"),
    ("338", "â„ï¸"),
    ("350", "ğŸŒ§"),
    ("353", "ğŸŒ¦"),
    ("356", "ğŸŒ§"),
    ("359", "ğŸŒ§"),
    ("362", "ğŸŒ§"),
    ("365", "ğŸŒ§"),
    ("368", "ğŸŒ¨"),
    ("371", "â„ï¸"),
    ("374", "ğŸŒ§"),
    ("377", "ğŸŒ§"),
    ("386", "â›ˆ"),
    ("389", "ğŸŒ©"),
    ("392", "â›ˆ"),
    ("395", "â„ï¸")
  ]
